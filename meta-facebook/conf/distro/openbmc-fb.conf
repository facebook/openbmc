# Facebook OpenBMC distro

# Use the base poky distro, but override various parameters.
require conf/distro/poky.conf
require conf/distro/include/fb-base.inc
DISTROOVERRIDES .= ":openbmc-fb"

# Extra image features.
EXTRA_IMAGE_FEATURES += "ssh-server-openssh tools-debug"
CORE_IMAGE_EXTRA_INSTALL += "ldd"

# List of supported distros we are using
SANITY_TESTED_DISTROS += " \
            CentOSLinux-7.[345].* \n \
            "

#
# Additional image features
#
# The following is a list of additional classes to use when building images which
# enable extra features. Some available options which can be included in this variable
# are:
#   - 'buildstats' collect build statistics
#   - 'image-swab' to perform host system intrusion detection
USER_CLASSES ?= "buildstats"

# Update root password to '0penBmc' and change the root shell back to bash.
# This default root password is used at the ODM and system integrator. It will be
# changed during provisioning at the datacenter.
INHERIT += "extrausers"

EXTRA_USERS_PARAMS += " \
  usermod -s /bin/bash root; \
  usermod -p '\$1\$UGMqyqdG\$FZiylVFmRRfl9Z0Ue8G7e/' root; \
  "

OPENBMC_FB_DEFAULT_FEATURE = "ext2 ipv6 largefile usbgadget usbhost nfs zeroconf package-management ipv4 ptest"
OPENBMC_BOARD_FEATURE ?= ""
DISTRO_FEATURES = "${DISTRO_FEATURES_LIBC} ${OPENBMC_FB_DEFAULT_FEATURE} ${OPENBMC_BOARD_FEATURE}"
DISTRO_FEATURES_BACKFILL_CONSIDERED = "\
    pulseaudio bluez5 gobject-introspection-data ldconfig"

# CONF_VERSION is increased each time build/conf/ changes incompatibly and is used to
# track the version of this file when it was generated. This can safely be ignored if
# this doesn't mean anything to you.
CONF_VERSION ?= "1"

HOSTTOOLS += "realpath"

PREFERRED_VERSION_asd ?= "0.1"
