# Delta OpenBMC distro

# Use the base poky distro, but override various parameters.
require conf/distro/poky.conf

INHERIT += "fb-source-mirror"

# Save local tarballs for all packages we download.
# This can be used to update our mirror directory above.
BB_GENERATE_MIRROR_TARBALLS = "1"

# Do not check reachability as it does not work in our environment
CONNECTIVITY_CHECK_URIS = ""

# The following setting will prevent bitbake from downloading anything over the
# network.  This can be used to ensure that we get everything from a local
# file:// mirror.
#
# Comment this out if you do need to download new packages from the internet.
# However, once you have downloaded the package you should check them into our
# mirror repository so that other developers will always get it from the mirror
# repo.
INHERIT += "fb-only-network"

# Parallelism Options
#
# How many tasks bitbake should run in parallel:
BB_NUMBER_THREADS ?= "${@oe.utils.cpu_count()}"
# How many processes make should run in parallel:
PARALLEL_MAKE ?= "-j ${@oe.utils.cpu_count()}"

# Build directory locationds.
#
DL_DIR ?= "/opt/yocto/downloads"
SSTATE_DIR ?= "${COREBASE}/../../../.sstate"
#TMPDIR = "${TOPDIR}/tmp"

# Use RPM packages
PACKAGE_CLASSES ?= "package_ipk"

# Extra image features.
EXTRA_IMAGE_FEATURES = "ssh-server-openssh tools-debug package-management"
CORE_IMAGE_EXTRA_INSTALL += "ldd"

# List of supported distros we are using
SANITY_TESTED_DISTROS += " \
            CentOSLinux-7.[345].* \n \
            "

#
# Additional image features
#
# The following is a list of additional classes to use when building images which
# enable extra features. Some available options which can be included in this variable
# are:
#   - 'buildstats' collect build statistics
#   - 'image-mklibs' to reduce shared library files size for an image
#   - 'image-prelink' in order to prelink the filesystem image
#   - 'image-swab' to perform host system intrusion detection
# NOTE: if listing mklibs & prelink both, then make sure mklibs is before prelink
# NOTE: mklibs also needs to be explicitly enabled for a given image, see local.conf.extended
USER_CLASSES ?= "buildstats image-mklibs image-prelink"

#
# Interactive shell configuration
#
# Under certain circumstances the system may need input from you and to do this it
# can launch an interactive shell. It needs to do this since the build is
# multithreaded and needs to be able to handle the case where more than one parallel
# process may require the user's attention. The default is iterate over the available
# terminal types to find one that works.
#
# Examples of the occasions this may happen are when resolving patches which cannot
# be applied, to use the devshell or the kernel menuconfig
#
# Supported values are auto, gnome, xfce, rxvt, screen, konsole (KDE 3.x only), none
# Note: currently, Konsole support only works for KDE 3.x due to the way
# newer Konsole versions behave
#OE_TERMINAL = "auto"
# By default disable interactive patch resolution (tasks will just fail instead):
PATCHRESOLVE = "noop"

#
# Disk Space Monitoring during the build
#
# Monitor the disk space during the build. If there is less that 1GB of space or less
# than 100K inodes in any key build location (TMPDIR, DL_DIR, SSTATE_DIR), gracefully
# shutdown the build. If there is less that 100MB or 1K inodes, perform a hard abort
# of the build. The reason for this is that running completely out of space can corrupt
# files and damages the build in ways which may not be easily recoverable.
BB_DISKMON_DIRS = "\
    STOPTASKS,${TMPDIR},1G,100K \
    STOPTASKS,${DL_DIR},1G,100K \
    STOPTASKS,${SSTATE_DIR},1G,100K \
    ABORT,${TMPDIR},100M,1K \
    ABORT,${DL_DIR},100M,1K \
    ABORT,${SSTATE_DIR},100M,1K"

#
# Shared-state files from other locations
#
# As mentioned above, shared state files are prebuilt cache data objects which can
# used to accelerate build time. This variable can be used to configure the system
# to search other mirror locations for these objects before it builds the data itself.
#
# This can be a filesystem directory, or a remote url such as http or ftp. These
# would contain the sstate-cache results from previous builds (possibly from other
# machines). This variable works like fetcher MIRRORS/PREMIRRORS and points to the
# cache locations to check for the shared objects.
# NOTE: if the mirror uses the same structure as SSTATE_DIR, you need to add PATH
# at the end as shown in the examples below. This will be substituted with the
# correct path within the directory structure.
SSTATE_MIRRORS ?= "\
  file://.* \
  file:///opt/yocto/sstate_mirror/PATH \
  file://.* \
  file:///data/users/openbmc/openbmc.sstatecache/LAST/.sstate/PATH \
  file://.* \
  file:///data/sandcastle/temp/openbmc_sstate/.sstate/PATH "

# Update root password to '0penBmc' and change the root shell back to bash.
# This default root password is used at the ODM and system integrator. It will be
# changed during provisioning at the datacenter.
INHERIT += "extrausers"

EXTRA_USERS_PARAMS += " \
  usermod -s /bin/bash root; \
  usermod -p '\$1\$UGMqyqdG\$FZiylVFmRRfl9Z0Ue8G7e/' root; \
  "

DEFAULT_TIMEZONE ?= "America/Los_Angeles"

# Get the OpenBMC verion
INHERIT += "openbmc_version"

OPENBMC_FB_DEFAULT_FEATURE = "ext2 ipv6 largefile usbgadget usbhost nfs zeroconf package-management ipv4 ptest"
OPENBMC_BOARD_FEATURE ?= ""
DISTRO_FEATURES = "${DISTRO_FEATURES_LIBC} ${OPENBMC_FB_DEFAULT_FEATURE} ${OPENBMC_BOARD_FEATURE}"
DISTRO_FEATURES_BACKFILL_CONSIDERED = "\
    pulseaudio bluez5 gobject-introspection-data ldconfig"

DISTRO_NAME = "Delta OpenBMC"
DISTRO_VERSION = "0.1"
TARGET_VENDOR = "-delta"
SDK_VENDOR = "-fbsdk"

MAINTAINER = "Delta OpenBMC Developers <leila.lin@deltaww.com>"

INHERIT += "blacklist"
PNBLACKLIST[busybox-syslog] = "We use rsyslog instead of busybox-syslog"
PNBLACKLIST[python] = "We use python3 only. Depend on python3 instead"

# CONF_VERSION is increased each time build/conf/ changes incompatibly and is used to
# track the version of this file when it was generated. This can safely be ignored if
# this doesn't mean anything to you.
CONF_VERSION = "1"

HOSTTOOLS += "realpath"

PREFERRED_VERSION_asd ?= "0.1"
