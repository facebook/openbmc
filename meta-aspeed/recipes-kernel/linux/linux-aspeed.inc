LINUX_VERSION_MAJOR ?= "${@d.getVar('LINUX_VERSION').split('.')[0]}"
LINUX_VERSION_MINOR ?= "${@d.getVar('LINUX_VERSION').split('.')[1]}"

require linux-aspeed-src.inc

DESCRIPTION = "Linux Kernel for Aspeed"
SECTION = "kernel"
LICENSE = "GPLv2"

COMPATIBLE_MACHINE = "aspeed"

do_compile[depends] = "libgcc:do_populate_sysroot"

inherit kernel

# linux-yocto.inc attempts to add kernel metadata, which we do not use.
# remove it to avoid the KERNEL_DANGLING_FEATURES warning.
KERNEL_FEATURES:remove = "cfg/fs/vfat.scc"

# Do not install kernel in rootfs
do_install[postfuncs] += "remove_kernel_image_from_rootfs"
remove_kernel_image_from_rootfs() {
    rm -rf ${D}/boot/${KERNEL_IMAGETYPE}*
}

pkg_postinst:kernel-image() {
}
